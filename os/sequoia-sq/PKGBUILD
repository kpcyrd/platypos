# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=sequoia-sq
pkgver=1.3.1
pkgrel=1
pkgdesc='Command-line frontends for Sequoia'
url='https://sequoia-pgp.org/'
arch=(x86_64)
license=(LGPL-2.0-or-later)
groups=(sequoia)
replaces=(sequoia)
depends=(
  bzip2 libbz2.so
  gcc-libs
  glibc
  gmp
  sqlite
)
makedepends=(
  capnproto
  cargo
  clang
  git
)
options=(!lto)
source=(git+https://gitlab.com/sequoia-pgp/sequoia-sq.git?signed#tag=v$pkgver)
sha512sums=('3eb0fbdab0a34f5732e8b0c1ee40f65427d09d15db83f3728ebc103e9085050d5436e546e1a4a73667620ba63fb7f557ddd14dd5785663e6314c573f9d2d1350')
b2sums=('e2409db64a5d3ffe15e5d1bc0367942212c036b89603643b8d266bf341fec5f31a418ea44dc84ed5c57ef531b98c584d6659618bf17da26d80cc246aadc8dc5d')
validpgpkeys=(
  CBCD8F030588653EEDD7E2659B7DD433F254904A  # Justus Winter <justus@sequoia-pgp.org>
  8F17777118A33DDA9BA48E62AACB3243630052D9  # Neal H. Walfield <neal@sequoia-pgp.org>
)

prepare() {
  cd $pkgname
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd $pkgname
  export CARGO_TARGET_DIR=../target
  export RUSTUP_TOOLCHAIN=stable
  export ASSET_OUT_DIR=../target
  cargo build --release --frozen --no-default-features --features crypto-rust --features sequoia-openpgp/allow-experimental-crypto --features sequoia-openpgp/allow-variable-time-crypto
}

#check() {
#  cd $pkgname
#  # NOTE: we use a different target dir, as otherwise cargo test --release alters the sq binary
#  # https://gitlab.com/sequoia-pgp/sequoia-sq/-/issues/96
#  export CARGO_TARGET_DIR=../target-test
#  export RUSTUP_TOOLCHAIN=stable
#  cargo test --release --frozen --features default
#}

package() {
  install -vDm 755 target/release/sq -t "$pkgdir/usr/bin"
  install -vDm 644 target/shell-completions/sq.bash "$pkgdir/usr/share/bash-completion/completions/sq"
  install -vDm 644 target/shell-completions/_sq -t "$pkgdir/usr/share/zsh/site-functions/"
  install -vDm 644 target/shell-completions/sq.fish -t "$pkgdir/usr/share/fish/vendor_completions.d/"
  install -vDm 644 target/man-pages/*.1 -t "$pkgdir/usr/share/man/man1/"
}

# vim: ts=2 sw=2 et:
