# Maintainer:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Jochem Kossen <j.kossen@home.nl>

pkgname=fakeroot
pkgver=1.37.1.2
pkgrel=1
pkgdesc='Tool for simulating superuser privileges'
arch=('x86_64')
license=('GPL-3.0-or-later')
url='https://tracker.debian.org/pkg/fakeroot'
install=fakeroot.install
depends=('glibc' 'filesystem' 'sed' 'util-linux' 'sh')
makedepends=(
  'autoconf'
  'automake'
  'git'
  'libtool'
  'systemd'
)
#checkdepends=('sharutils')
source=("git+https://salsa.debian.org/clint/fakeroot.git#tag=upstream/${pkgver}")
sha256sums=('41dda8c3dd030b9ee4d159e3f76150b3bef3ecac37ad89e4684b68e41c94159e')

prepare() {
  cd "${pkgname}"

  sed -i 's/SUBDIRS = .*//' doc/Makefile.am
  autoreconf -fi
}

build() {
  cd "${pkgname}"

  ./configure \
    --prefix=/usr \
    --libdir=/usr/lib/libfakeroot \
    --disable-static \
    --with-ipc=sysv
  make
}

#check() {
#  cd "${pkgname}"
#
#  make check
#}

package() {
  cd "${pkgname}"

  make DESTDIR="${pkgdir}" install

  install -dm0755 "${pkgdir}/usr/lib/ld.so.conf.d/"
  echo '/usr/lib/libfakeroot' > "${pkgdir}/usr/lib/ld.so.conf.d/fakeroot.conf"

  # install README for sysv/tcp usage
  install -Dm0644 README "${pkgdir}/usr/share/doc/${pkgname}/README"
}
