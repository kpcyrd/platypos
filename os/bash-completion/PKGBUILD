# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: Eric BÃ©langer <eric@archlinux.org>

pkgname=bash-completion
pkgver=2.16.0
pkgrel=1
pkgdesc='Programmable completion for the bash shell'
arch=(any)
url='https://github.com/scop/bash-completion'
license=(GPL-2.0-only)
depends=(bash)
options=(!emptydirs !makeflags)
backup=('etc/bash_completion.d/000_bash_completion_compat.bash')
source=($url/releases/download/$pkgver/$pkgname-$pkgver.tar.xz)
validpgpkeys=(A558062B6D5DA1347CD468D52B0AE2C1B09B07CB)
b2sums=('389be47536f8f9aa383ff85bf19a00562b30b6d3e3652feaa01284987af13c2dc9850b59341956dd8e94e54251cd36902a60d5e3420ebfa2567216546144f675')

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  # bash-completion is sourced in /etc/bash.bashrc so that non-bash shell don't source it
  rm "${pkgdir}/etc/profile.d/bash_completion.sh"

  # remove Slackware's makepkg completion
  rm "${pkgdir}/usr/share/bash-completion/completions/makepkg"
  # remove libsecret provided completion issue #3
  rm "${pkgdir}/usr/share/bash-completion/completions/secret-tool" 

  # remove uutils conflicts
  for prog in arch chgrp chmod chown dd env hostname id kill mktemp nproc printenv pwd timeout truncate; do
    rm "${pkgdir}/usr/share/bash-completion/completions/${prog}"
  done
}

# vim: ts=2 sw=2 et:
