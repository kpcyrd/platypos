# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Dave Reisner <dreisner@archlinux.org>

pkgname=arch-install-scripts
pkgver=29
pkgrel=1
pkgdesc="Scripts to aid in installing Arch Linux"
arch=('any')
url="https://gitlab.archlinux.org/archlinux/arch-install-scripts"
license=('GPL-2.0-only')
depends=('awk' 'bash' 'coreutils' 'grep' 'pacman' 'util-linux')
makedepends=(
  'git'
  'm4'
  'make'
)
source=("git+$url.git#tag=v${pkgver}?signed")
validpgpkeys=(
  'C100346676634E80C940FB9E9C02FF419FECBE16'  # Morten Linderud
)
sha256sums=('8b362ae2e24916889f128e08298b010ea5bca4085e45b64105aeb1d212d20600')

prepare() {
  cd "$pkgname"
  sed -i 's/^man: .*/man:/' Makefile
  sed -i '\#share/man/man8#d' Makefile
}

build() {
  make -C "$pkgname"
}

#check() {
#  make -C "$pkgname" check
#}

package() {
  make -C "$pkgname" PREFIX=/usr DESTDIR="$pkgdir" install
}
