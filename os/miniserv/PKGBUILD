# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>

pkgname=miniserve
pkgver=0.29.0
pkgrel=2
pkgdesc="Tool to serve files via HTTP"
arch=('x86_64')
url="https://github.com/svenstaro/miniserve"
license=('MIT')
depends=('gcc-libs' 'bzip2')
makedepends=('rust')
options=(!lto)
source=(miniserve-$pkgver.tar.gz::https://github.com/svenstaro/miniserve/archive/v${pkgver}.tar.gz)
sha512sums=('45fea804ab757d4d9c878cfc2969645783aa1b1b3b2ab8bb7bd3a79e27787658b8f1a75bf9c390ae31c9c0e8e09b55bf3ea87bb154db606241786fa3770974b7')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  cargo build --release --locked
}

#check() {
#  cd "$srcdir/$pkgname-$pkgver"
#
#  cargo test --release --locked -- --test-threads=1
#}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  install -Dm755 target/release/miniserve "$pkgdir"/usr/bin/miniserve

  mkdir -p "$pkgdir"/usr/share/{bash-completion/completions,zsh/site-functions,fish/vendor_completions.d}
  target/release/miniserve --print-completions bash > "$pkgdir"/usr/share/bash-completion/completions/miniserve
  target/release/miniserve --print-completions zsh > "$pkgdir"/usr/share/zsh/site-functions/_miniserve
  target/release/miniserve --print-completions fish > "$pkgdir"/usr/share/fish/vendor_completions.d/miniserve.fish

  mkdir -p "$pkgdir"/usr/share/man/man1
  target/release/miniserve --print-manpage | gzip > "$pkgdir"/usr/share/man/man1/$pkgname.1.gz

  install -Dm644 packaging/miniserve@.service "$pkgdir"/usr/lib/systemd/system/miniserve@.service

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
